group 'com.example.espresso'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.2'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    namespace 'com.example.espresso'
    compileSdk 34

    defaultConfig {
        minSdkVersion 19
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    lintOptions {
        checkAllWarnings true
        warningsAsErrors true
        disable 'AndroidGradlePluginVersion', 'InvalidPackage', 'GradleDependency', 'NewerVersionAvailable'
        baseline file("lint-baseline.xml")
    }


    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

dependencies {
    implementation 'com.google.guava:guava:33.3.1-android'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    androidTestImplementation 'org.hamcrest:hamcrest:3.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.4.4"
    api 'androidx.test.espresso:espresso-core:3.6.1'

    // Core library
    api 'androidx.test:core:1.6.1'

    // AndroidJUnitRunner and JUnit Rules
    api 'androidx.test:runner:1.6.1'
    api 'androidx.test:rules:1.6.1'

    // Assertions
    api 'androidx.test.ext:junit:1.2.1'
    api 'androidx.test.ext:truth:1.6.0'
    api 'com.google.truth:truth:1.4.4'

    // Espresso dependencies
    api 'androidx.test.espresso:espresso-core:3.6.1'
    api 'androidx.test.espresso:espresso-contrib:3.6.1'
    api 'androidx.test.espresso:espresso-intents:3.6.1'
    api 'androidx.test.espresso:espresso-accessibility:3.6.1'
    api 'androidx.test.espresso:espresso-web:3.6.1'
    api 'androidx.test.espresso.idling:idling-concurrent:3.6.1'

    // The following Espresso dependency can be either "implementation"
    // or "androidTestImplementation", depending on whether you want the
    // dependency to appear on your APK's compile classpath or the test APK
    // classpath.
    api 'androidx.test.espresso:espresso-idling-resource:3.6.1'
}


