group 'io.flutter.plugins.camerax'
version '1.0'

buildscript {
    ext.kotlin_version = '1.9.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace 'io.flutter.plugins.camerax'
    // CameraX dependencies require compilation against version 33 or later.
    compileSdk = flutter.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        // This must match the Java version provided in compileOptions.
        jvmTarget = '11'
    }

    defaultConfig {
        // Many of the CameraX APIs require API 21.
        minSdkVersion 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            // The org.gradle.jvmargs property that may be set in gradle.properties does not impact
            // the Java heap size when running the Android unit tests. The following property here
            // sets the heap size to a size large enough to run the robolectric tests across
            // multiple SDK levels.
            jvmArgs "-Xmx1G"
            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }

    lint {
        checkAllWarnings true
        warningsAsErrors true
        disable 'AndroidGradlePluginVersion', 'GradleDependency', 'InvalidPackage', 'NewerVersionAvailable'
        baseline = file("lint-baseline.xml")
    }
}

dependencies {
    // CameraX core library using the camera2 implementation must use same version number.
    def camerax_version = "1.5.0-beta01"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-video:${camerax_version}"
    implementation 'com.google.guava:guava:33.4.0-android'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:5.18.0"
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'androidx.test:core:1.7.0'
    testImplementation 'org.robolectric:robolectric:4.15.1'
}
