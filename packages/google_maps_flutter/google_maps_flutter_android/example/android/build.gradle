buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

allprojects {
    repositories {
        // See https://github.com/flutter/flutter/blob/master/docs/ecosystem/Plugins-and-Packages-repository-structure.md#gradle-structure for more info.
        def artifactRepoKey = 'ARTIFACT_HUB_REPOSITORY'
        if (System.getenv().containsKey(artifactRepoKey)) {
            println "Using artifact hub"
            maven { url System.getenv(artifactRepoKey) }
        }
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

// Build the plugin project with warnings enabled. This is here rather than
// in the plugin itself to avoid breaking clients that have different
// warnings (e.g., deprecation warnings from a newer SDK than this project
// builds with).
gradle.projectsEvaluated {
    project(":google_maps_flutter_android") {
        tasks.withType(JavaCompile) {
            // Ignore classfile warnings due to https://bugs.openjdk.org/browse/JDK-8190452
            // TODO(stuartmorgan): Remove that ignore once the build uses Java 11+.
            options.compilerArgs << "-Xlint:all" << "-Werror" << "-Xlint:-classfile"
            // Workaround for a warning when building that the above turns into
            // an error, coming from jetified-play-services-maps-18.1.0:
            //   warning: Cannot find annotation method 'value()' in type
            //   'GuardedBy': class file for
            //   javax.annotation.concurrent.GuardedBy not found
            dependencies {
                implementation "com.google.code.findbugs:jsr305:3.0.2"
            }
        }
    }
}
